# Modified by ROllerozxa for the MilkyTracker Discord RPC fork.
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Christopher O'Neill <milkytracker@chrisoneill.co.uk>
# Contributor: Maks Verver <maksverver@geocities.com>

pkgname=milkytracker-rollerozxa
_pkgname=milkytracker
pkgver=1.03.00.roller1
_pkghash=a37a917aec4bf89c0c87f5cc9786eca8975b3b75
pkgrel=2
pkgdesc='Music tracker inspired by Fast Tracker II'
arch=(x86_64)
url='https://milkytracker.org/'
license=(GPL3)
depends=(alsa-lib lhasa rtmidi sdl2 zziplib discord-rpc-api)
makedepends=(cmake jack ninja)
optdepends=('jack: JACK audio support')

provides=(milkytracker)
conflicts=(milkytracker)

source=("$_pkgname-$_pkghash.tar.gz::https://github.com/rollerozxa/MilkyTracker/archive/$_pkghash.tar.gz")
sha256sums=('SKIP')

build() {
	mkdir -p build
	cd build
	cmake "../MilkyTracker-$_pkghash" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-G Ninja
	ninja
}

package() {
	cd build
	DESTDIR="$pkgdir" ninja install
	cd "$srcdir/MilkyTracker-$_pkghash/resources"
	install -Dm644 pictures/carton.png \
		"$pkgdir/usr/share/pixmaps/milkytracker.png"
	install -Dm644 milkytracker.desktop \
		"$pkgdir/usr/share/applications/milkytracker.desktop"
	# yes, this file extension is wrong
	install -Dm644 milkytracker.appdata \
		"$pkgdir/usr/share/appdata/milkytracker.appdata.xml"
}
